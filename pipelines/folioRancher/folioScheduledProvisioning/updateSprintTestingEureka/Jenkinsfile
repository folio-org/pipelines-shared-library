#!groovy
import groovy.transform.Field
import org.folio.Constants
import org.folio.models.parameters.CreateNamespaceParameters
import org.folio.rest_v2.PlatformType
import org.folio.utilities.GitHubClient
import org.jenkinsci.plugins.workflow.libs.Library

import java.time.DayOfWeek
import java.time.LocalDate
import java.time.temporal.ChronoUnit

@Library('pipelines-shared-library') _

@Field final String createNamespaceJobName = Constants.JENKINS_CREATE_NAMESPACE_FROM_BRANCH_JOB
@Field final String deleteNamespaceJobName = Constants.JENKINS_DELETE_NAMESPACE_JOB

@Field final LocalDate today = LocalDate.now()
@Field final LocalDate thisWednesday = today.with(java.time.temporal.TemporalAdjusters.nextOrSame(DayOfWeek.WEDNESDAY))
@Field final LocalDate lastWednesday = thisWednesday.minusWeeks(1)
@Field final LocalDate nextWednesday = thisWednesday.plusWeeks(1)

@Field final int weeksSinceEpoch = (int)(java.time.temporal.ChronoUnit.WEEKS.between(LocalDate.of(2026, 1, 14), thisWednesday))
@Field final boolean isSecondWednesday =
  today.getDayOfWeek() == DayOfWeek.WEDNESDAY && (weeksSinceEpoch % 2 == 0)

properties([
  buildDiscarder(logRotator(numToKeepStr: '30')),
  disableConcurrentBuilds(),
  parameters([
    folioParameters.platform(),
    folioParameters.cluster('PLATFORM'),
    folioParameters.namespace(),
    folioParameters.configType(),
    string(name: 'DB_BACKUP_NAME', defaultValue: 'sprint-testing-prep-8-18-2025', description: 'Set DB backup name'),
    booleanParam(name: 'MARC_MIGRATIONS', defaultValue: false, description: 'enable marc-migrations'),
    choice(name: 'TYPE', choices: ['full', 'terraform', 'update'], description: '(Required) Set action TYPE to perform'),
    booleanParam(name: 'IS_CRON_JOB', defaultValue: false, description: '(Hidden) Is job triggered by cron job'),
    folioParameters.refreshParameters()
    , folioParameters.hideParameters(['IS_CRON_JOB'])

  ]),
  pipelineTriggers([
    parameterizedCron('''
      H 3 * * 1,2,3,4,5 %PLATFORM=EUREKA;CLUSTER=folio-etesting;NAMESPACE=sprint;CONFIG_TYPE=testing;DB_BACKUP_NAME=sprint-testing-prep-8-18-2025;MARC_MIGRATIONS=true;TYPE=update;IS_CRON_JOB=true
      H 0 * * 3 %PLATFORM=EUREKA;CLUSTER=folio-etesting;NAMESPACE=sprint;CONFIG_TYPE=testing;DB_BACKUP_NAME=sprint-testing-prep-8-18-2025;MARC_MIGRATIONS=true;TYPE=full;IS_CRON_JOB=true
    ''')])
])


if (params.REFRESH_PARAMETERS) {
  currentBuild.result = 'ABORTED'
  return
}

if (params.IS_CRON_JOB) {
  if (isSecondWednesday && params.TYPE != 'full') {
    echo "REASON: Second Wednesday but TYPE is '${params.TYPE}' (expected 'full')"
    currentBuild.result = 'ABORTED'
    return
  } else if (!isSecondWednesday && params.TYPE != 'update') {
    echo "REASON: Not second Wednesday but TYPE is '${params.TYPE}' (expected 'update')"
    currentBuild.result = 'ABORTED'
    return
  }
  echo "Proceeding with execution - conditions met"
}

CreateNamespaceParameters namespace = new CreateNamespaceParameters.Builder()
  .platform(PlatformType.EUREKA)
  .clusterName(params.CLUSTER)
  .namespaceName(params.NAMESPACE)
  .folioBranch('snapshot')
  .platformBranch('snapshot')
  .okapiVersion('latest')
  .configType(params.CONFIG_TYPE)
  .loadReference(false)
  .loadSample(false)
  .consortia(true)
  .linkedData(true)
  .rwSplit(false)
  .greenmail(false)
  .mockServer(false)
  .rtr(false)
  .splitFiles(true)
  .ecsCCL(false)
  .dbBackupName(params.DB_BACKUP_NAME)
  .doMarcMigrations(params.MARC_MIGRATIONS)
  .type(params.TYPE)
  .hasSecureTenant(false)
  .secureTenantId(folioDefault.consortiaTenants().get('university').getTenantId())
  .dataset(true)
  .pgType('aws')
  .pgVersion('16.8')
  .kafkaType('aws')
  .opensearchType('aws')
  .s3Type('aws')
  .scNative(true)
  .uiBuild(false)
  .runSanityCheck(false)
  .members('thunderjet,folijet,spitfire,vega,thor,Eureka,volaris,corsair,Bama,Aggies,Dreamliner,Leipzig,firebird,dojo,erm')
  .build()

ansiColor('xterm') {
  stage('Fetch Applications') {
    Map platformDescriptor = new GitHubClient(this).getRepoFileContent('platform-lsp', 'snapshot', 'platform-descriptor.json') as Map
    List<String> appNames = platformDescriptor.applications?.required?.collect { it.name } ?: []
    appNames += platformDescriptor.applications?.optional?.collect { it.name } ?: []
    if (!params.MARC_MIGRATIONS) {
      appNames.remove('app-marc-migrations')
    }
    println "Found ${appNames.size()} applications in platform-descriptor.json"
    namespace.applications = appNames
  }

  stage('Update Sprint Testing') {
    if(params.TYPE == 'full' ) {
      folioTriggerJob.deleteNamespace(deleteNamespaceJobName, namespace)
    }

    folioTriggerJob.createNamespaceFromBranch(createNamespaceJobName, namespace)
  }
}
