#!groovy
import org.folio.Constants
import org.folio.jenkins.JenkinsAgentLabel
import org.folio.jenkins.PodTemplates
import org.folio.models.OkapiTenant
import org.folio.models.OkapiUser
import org.folio.models.RancherNamespace
import org.folio.models.parameters.CreateNamespaceParameters
import org.folio.models.parameters.CypressTestsParameters

@Library('pipelines-shared-library@RANCHER-650-Jenkins-upgrade') _

properties([
  buildDiscarder(logRotator(numToKeepStr: '30')),
  parameters([
    folioParameters.cluster(),
    folioParameters.namespace(),
    folioParameters.branch(),
    folioParameters.okapiVersion(),
    folioParameters.configType(),
    folioParameters.loadReference(),
    folioParameters.loadSample(),
    booleanParam(name: 'CONSORTIA', defaultValue: true, description: '(Optional) Set true to create consortium'),
    booleanParam(name: 'LINKED_DATA', defaultValue: true, description: '(Optional) Set true to deploy LinkedData'),
    booleanParam(name: 'SPLIT_FILES', defaultValue: true, description: '(Optional) Set true to enable split-files functionality'),
    booleanParam(name: 'RW_SPLIT', defaultValue: false, description: '(Optional) Set true to Enable Read/Write split'),
    booleanParam(name: 'GREENMAIL', defaultValue: false, description: '(Optional) Set true to deploy greenmail server'),
    booleanParam(name: 'MOCK_SERVER', defaultValue: false, description: '(Optional) Set true to deploy mock-server'),
    booleanParam(name: 'RTR', defaultValue: false, description: '(Optional) Set true to enable RTR'),
    folioParameters.pgType(),
    folioParameters.pgVersion(),
    folioParameters.kafkaType(),
    folioParameters.opensearchType(),
    folioParameters.s3Type(),
    folioParameters.runSanityCheck(),
    folioParameters.refreshParameters()
  ])
])

if (params.REFRESH_PARAMETERS) {
  currentBuild.result = 'ABORTED'
  return
}

if (params.CLUSTER == 'folio-testing') {
  folioCommon.kitfoxApproval()
}

CreateNamespaceParameters namespaceParams = new CreateNamespaceParameters.Builder()
  .clusterName(params.CLUSTER)
  .namespaceName(params.NAMESPACE)
  .folioBranch(params.FOLIO_BRANCH)
  .okapiVersion(params.OKAPI_VERSION)
  .configType(params.CONFIG_TYPE)
  .loadReference(params.LOAD_REFERENCE)
  .loadSample(params.LOAD_SAMPLE)
  .consortia(params.CONSORTIA)
  .splitFiles(params.SPLIT_FILES)
  .rwSplit(params.RW_SPLIT)
  .linkedData(params.LINKED_DATA)
  .greenmail(params.GREENMAIL)
  .mockServer(params.MOCK_SERVER)
  .rtr(params.RTR)
  .pgType(params.POSTGRESQL)
  .pgVersion(params.DB_VERSION)
  .kafkaType(params.KAFKA)
  .opensearchType(params.OPENSEARCH)
  .s3Type(params.S3_BUCKET)
  .build()

if (params.CONSORTIA) {
  namespaceParams.folioExtensions.add('consortia')
}

if (params.LINKED_DATA) {
  namespaceParams.folioExtensions.add('linked-data')
}

RancherNamespace namespace = new RancherNamespace(namespaceParams.getClusterName(), namespaceParams.getNamespaceName())

CypressTestsParameters cypressSanityParameters = new CypressTestsParameters()
cypressSanityParameters.setCiBuildId(env.JOB_BASE_NAME)
cypressSanityParameters.setTestsSrcBranch('okapi')
cypressSanityParameters.setTenantUrl("https://${namespace.generateDomain('diku')}")
cypressSanityParameters.setOkapiUrl("https://${namespace.getDomains().okapi}")
cypressSanityParameters.setTenant(new OkapiTenant('diku')
  .withAdminUser(new OkapiUser('diku_admin', 'admin')))
cypressSanityParameters.setExecParameters('--env grepTags="fse+sanity"')
cypressSanityParameters.setWorkerLabel('cypress')
cypressSanityParameters.setNumberOfWorkers(4)
cypressSanityParameters.setTimeout('60')

ansiColor('xterm') {
  PodTemplates podTemplates = new PodTemplates(this)

  podTemplates.defaultTemplate {
    node(JenkinsAgentLabel.DEFAULT_AGENT.getLabel()) {
      try {
        stage('Ini') {
          buildName "${params.CLUSTER}-${params.NAMESPACE}.${env.BUILD_ID}"
          buildDescription "Branch: ${params.FOLIO_BRANCH}\nConfig: ${params.CONFIG_TYPE}\n" +
            "DB version: ${params.DB_VERSION}\nRW split: ${params.RW_SPLIT}\nGreenmail: ${params.POSTGRESQL}\n" +
            "Mock server: ${params.MOCK_SERVER}\nConsortia: ${params.CONSORTIA}"
        }

        stage('Checkout') {
          git(url: "${Constants.FOLIO_GITHUB_URL}/pipelines-shared-library.git",
            branch: folioTools.getPipelineBranch(),
            credentialsId: Constants.PRIVATE_GITHUB_CREDENTIALS_ID,
            poll: false)
        }

        folioNamespaceCreate(namespaceParams)

        stage('[Cypress] Run sanity check') {
          if (params.RUN_SANITY_CHECK) {
            folioCypressFlow.call(cypressSanityParameters.getCiBuildId(), [cypressSanityParameters])
          }
        }

      } catch (e) {
        /** Put error message to the Job Run description */
        currentBuild.description = e.getMessage()
        currentBuild.result = 'FAILURE'

        println "Caught exception: ${e}"
        error(e.getMessage())
      }
    }
  }
}
