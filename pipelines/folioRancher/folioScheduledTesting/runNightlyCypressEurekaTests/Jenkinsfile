#!groovy
import groovy.transform.Field
import org.folio.Constants
import org.folio.jenkins.PodTemplates
import org.folio.models.OkapiTenant
import org.folio.models.OkapiUser
import org.folio.models.RancherNamespace
import org.folio.models.parameters.CreateNamespaceParameters
import org.folio.models.parameters.CypressTestsParameters
import org.folio.rest_v2.PlatformType
import org.folio.utilities.Logger
import org.jenkinsci.plugins.workflow.libs.Library

@Library('pipelines-shared-library') _

/**
 * Job properties and parameters
 */
properties([
  pipelineTriggers([cron('H 21 * * 1-5')]),
  buildDiscarder(logRotator(numToKeepStr: '30')),
  disableConcurrentBuilds(),
  parameters([
    string(name: 'CYPRESS_BRANCH', defaultValue: 'master', description: "(Required) Insert what 'stripes-testing' branch use for build"),
    string(name: 'NUMBER_WORKERS', defaultValue: '15', description: "(Required) Define number of workers for Cypress tests execution"),
    folioParameters.refreshParameters()
  ])
])

if (params.REFRESH_PARAMETERS) {
  currentBuild.result = 'ABORTED'
  return
}

/**
 * Job variables
 */
PodTemplates podTemplates = new PodTemplates(this)
Logger logger = new Logger(this, env.JOB_BASE_NAME)

@Field final String deleteNamespaceJobName = Constants.JENKINS_DELETE_NAMESPACE_JOB
@Field final String createNamespaceJobName = Constants.JENKINS_CREATE_NAMESPACE_FROM_BRANCH_JOB

final String platformCompleteBranch = 'snapshot'

CreateNamespaceParameters namespaceBaseParams = new CreateNamespaceParameters.Builder()
  .platform(PlatformType.EUREKA)
  .clusterName('folio-etesting')
  .namespaceName('cypress')
  .rwSplit(false)
  .greenmail(false)
  .mockServer(false)
  .pgType('built-in')
  .pgVersion('16.8')
  .kafkaType('aws')
  .opensearchType('aws')
  .s3Type('built-in')
  .members('AQA')
  .build()

CreateNamespaceParameters namespaceFromBranchParams = namespaceBaseParams.toBuilder()
  .folioBranch(platformCompleteBranch)
  .okapiVersion('latest')
  .configType('testing')
  .loadReference(true)
  .loadSample(true)
  .linkedData(true)
  .consortia(true)
  .rtr(false)
  .scNative(true)
  .applicationSet('Complete')
  .applications(org.folio.rest_v2.Constants.APPLICATION_BRANCH('Complete'))
  .hasSecureTenant(true)
  .secureTenantId(folioDefault.consortiaTenants().get('university').getTenantId())
  .uiBuild(true)
  .build()

RancherNamespace namespace = new RancherNamespace(namespaceBaseParams.getClusterName(), namespaceBaseParams.getNamespaceName())

CypressTestsParameters cypressParameters = new CypressTestsParameters()
cypressParameters.setCiBuildId(env.JOB_BASE_NAME)
cypressParameters.setTestsSrcBranch(params.CYPRESS_BRANCH)
cypressParameters.setTenantUrl("https://${namespace.generateDomain('diku')}")
cypressParameters.setOkapiUrl("https://${namespace.getDomains().kong}")
cypressParameters.setTenant(new OkapiTenant('diku')
  .withAdminUser(new OkapiUser('diku_admin', 'admin')))
cypressParameters.setExecParameters('--env grepTags="smoke criticalPath extendedPath"')
cypressParameters.setTestrailProjectID('14')
cypressParameters.setTestrailRunID('2723')
cypressParameters.setNumberOfWorkers(params.NUMBER_WORKERS.toInteger())
cypressParameters.setTimeout('1080')

boolean reportPortalUse = true
String reportPortalRunType = 'evrk'

/**
 * Pipeline
 */
ansiColor('xterm') {
  podTemplates.rancherAgent {
    stage('Ini') {
      buildDescription "Branch: ${params.CYPRESS_BRANCH}\nConfig: ${namespaceFromBranchParams.getConfigType()}\nEnv: ${cypressParameters.tenantUrl}"
    }

    stage('[Job] Destroy environment') {
      def nsExists
      folioHelm.withKubeConfig(namespaceBaseParams.getClusterName()) {
        nsExists = kubectl.checkNamespaceExistence("${namespaceBaseParams.getNamespaceName()}")
      }
      if (nsExists) {
        retry(2) {
          folioTriggerJob.deleteNamespace(deleteNamespaceJobName, namespaceBaseParams)
        }
      } else {
        logger.info("${namespaceBaseParams.getNamespaceName()} namespace does not exists!")
      }
    }

    stage('[Job] Provision environment') {
      try {
        folioTriggerJob.createNamespaceFromBranch(createNamespaceJobName, namespaceFromBranchParams)
      } catch (Exception new_ex) {
        slackSend(attachments: folioSlackNotificationUtils.renderFailedBuildResultMessage()
          , channel: "#rancher_tests_notifications")
        throw new Exception("Provision of the environment is failed: " + new_ex)
      }
    }
    folioCypressFlow.call(cypressParameters.getCiBuildId(), [cypressParameters], false, reportPortalUse, reportPortalRunType)
  }
}
