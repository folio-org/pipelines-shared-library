#!groovy

import org.folio.Constants
import org.folio.jenkins.JenkinsAgentLabel
import org.folio.jenkins.PodTemplates
import org.folio.models.OkapiTenant
import org.folio.models.OkapiUser
import org.folio.models.RancherNamespace
import org.folio.models.parameters.CreateNamespaceParameters
import org.folio.models.parameters.CypressTestsParameters
import org.folio.utilities.Logger
import org.jenkinsci.plugins.workflow.libs.Library

@Library('pipelines-shared-library@RANCHER-2236') _

/**
 * Job properties and parameters
 */
properties([
//  pipelineTriggers([cron('H 21 * * 1,3,5')]),
  buildDiscarder(logRotator(numToKeepStr: '30')),
  disableConcurrentBuilds(),
  parameters([
    string(name: 'CYPRESS_BRANCH', defaultValue: 'okapi', description: "(Required) Insert what 'stripes-testing' branch use for build"),
    folioParameters.refreshParameters()
  ])
])

if (params.REFRESH_PARAMETERS) {
  currentBuild.result = 'ABORTED'
  return
}

/**
 * Job variables
 */
Logger logger = new Logger(this, env.JOB_BASE_NAME)

final String createNamespaceJobName = Constants.JENKINS_CREATE_NAMESPACE_FROM_BRANCH_JOB
final String deleteNamespaceJobName = Constants.JENKINS_DELETE_NAMESPACE_JOB
final String platformCompleteBranch = 'snapshot'
String okapiVersion = folioTools.eval(folioStringScripts.getOkapiVersions(), ['FOLIO_BRANCH': platformCompleteBranch])[0]

CreateNamespaceParameters namespaceBaseParams = new CreateNamespaceParameters.Builder()
  .clusterName('folio-testing')
  .namespaceName('cypress')
  .rwSplit(false)
  .greenmail(false)
  .mockServer(false)
  .pgType('built-in')
  .pgVersion('16.1')
  .kafkaType('aws')
  .opensearchType('aws')
  .s3Type('built-in')
  .members('')
  .build()

CreateNamespaceParameters namespaceFromBranchParams = namespaceBaseParams.toBuilder()
  .folioBranch(platformCompleteBranch)
  .okapiVersion(okapiVersion)
  .configType('testing')
  .loadReference(true)
  .loadSample(true)
  .consortia(true)
  .linkedData(true)
  .splitFiles(true)
  .ecsCCL(false)
  .rtr(false)
  .build()

RancherNamespace namespace = new RancherNamespace(namespaceBaseParams.getClusterName(), namespaceBaseParams.getNamespaceName())

CypressTestsParameters cypressParameters = new CypressTestsParameters()
cypressParameters.setCiBuildId(env.JOB_BASE_NAME)
cypressParameters.setTestsSrcBranch(params.CYPRESS_BRANCH)
cypressParameters.setTenantUrl("https://${namespace.generateDomain('diku')}")
cypressParameters.setOkapiUrl("https://${namespace.getDomains().okapi}")
cypressParameters.setTenant(new OkapiTenant('diku')
  .withAdminUser(new OkapiUser('diku_admin', 'admin')))
cypressParameters.setExecParameters('--env grepTags="smoke criticalPath"')
cypressParameters.setTestrailProjectID('14')
cypressParameters.setTestrailRunID('2108')
cypressParameters.setNumberOfWorkers(12)
cypressParameters.setTimeout('480')

boolean reportPortalUse = true
String reportPortalRunType = 'night'

/**
 * Pipeline
 */
ansiColor('xterm') {
  PodTemplates podTemplates = new PodTemplates(this)

  podTemplates.defaultTemplate {
    node(JenkinsAgentLabel.DEFAULT_AGENT.getLabel()) {
      stage('Ini') {
        buildDescription "Branch: ${params.CYPRESS_BRANCH}\nEnv: ${cypressParameters.tenantUrl}"
      }

      stage('Approve') {
        timeout(time: 60, unit: 'MINUTES') {
          folioCommon.kitfoxApproval()
        }
      }

      stage('[Job] Destroy environment') {
        def nsExists
        folioHelm.withKubeConfig(namespaceBaseParams.getClusterName()) {
          nsExists = kubectl.checkNamespaceExistence("${namespaceBaseParams.getNamespaceName()}")
        }
        if (nsExists) {
          retry(2) {
            folioTriggerJob.deleteNamespace(deleteNamespaceJobName, namespaceBaseParams)
          }
        } else {
          logger.info("${namespaceBaseParams.getNamespaceName()} namespace does not exists!")
        }
      }

      stage('[Job] Provision environment') {
        try {
          folioTriggerJob.createNamespaceFromBranch(createNamespaceJobName, namespaceFromBranchParams)
        } catch (Exception new_ex) {
          slackSend(attachments: folioSlackNotificationUtils.renderFailedBuildResultMessage()
            , channel: "#rancher_tests_notifications")
          throw new Exception("Provision of the environment is failed: " + new_ex)
        }
      }
    }
  }

  folioCypressFlow.call(cypressParameters.getCiBuildId(), [cypressParameters], false, reportPortalUse, reportPortalRunType)
}
