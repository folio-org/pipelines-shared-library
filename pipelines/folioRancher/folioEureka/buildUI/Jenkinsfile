#!groovy
import org.folio.Constants
import org.folio.jenkins.PodTemplates
import org.folio.models.EurekaNamespace
import org.folio.models.EurekaTenant
import org.folio.models.OkapiConfig
import org.folio.models.TenantUi
import org.folio.models.module.FolioModule
import org.jenkinsci.plugins.workflow.libs.Library

//TODO remove branch before merge to master
@Library('pipelines-shared-library@RANCHER-2735') _

properties([
  buildDiscarder(logRotator(numToKeepStr: '30')),
//  disableConcurrentBuilds(),
  parameters([
    folioParameters.cluster(),
    folioParameters.namespace(),
    folioParameters.configType(),
    folioParameters.branch('FOLIO_BRANCH', 'platform-lsp'),
    string(name: 'CUSTOM_HASH', defaultValue: '', description: 'Commit hash for bundle build from specific commit'),
    folioParameters.tenantId(),
    booleanParam(name: 'CONSORTIA', defaultValue: false, description: '(Optional) Enable Consortia'),
    booleanParam(name: 'CONSORTIA_SINGLE_UI', defaultValue: true, description: '(Optional) Set true to build Single UX bundle, instead of Drop down for ECS'),
    booleanParam(name: 'LINKED_DATA', defaultValue: false, description: '(Optional) Include linked-data module in UI bundle'),
    booleanParam(name: 'ECS_CCL', defaultValue: false, description: '(Optional) Set true to enable ECS Circulation + Congressional loans functionality'),
    folioParameters.refreshParameters()])
])

if (params.REFRESH_PARAMETERS) {
  currentBuild.result = 'ABORTED'
  return
}

if (params.NAMESPACE in Constants.RANCHER_PROTECTED_NAMESPACES) {
  folioCommon.kitfoxApproval()
}

List<FolioModule> removeUIComponents = []
if (!params.CONSORTIA) {
  removeUIComponents.add(new FolioModule().loadModuleDetails('folio_consortia-settings-0.0.0'))
}

if (!params.LINKED_DATA) {
  removeUIComponents.add(new FolioModule().loadModuleDetails('folio_ld-folio-wrapper-0.0.0'))
}

/**
 * Job variables
 **/
final String FOLIO_REPOSITORY = 'platform-lsp'
PodTemplates podTemplates = new PodTemplates(this)
boolean isRelease = params.FOLIO_BRANCH ==~ /^R\d-\d{4}.*/
String commitHash = params.CUSTOM_HASH?.trim() ? params.CUSTOM_HASH : common.getLastCommitHash(FOLIO_REPOSITORY, params.FOLIO_BRANCH)

EurekaNamespace namespace = new EurekaNamespace("${params.CLUSTER}", "${params.NAMESPACE}")
  .withDefaultTenant("${params.TENANT_ID}")
  .withDeploymentConfigType(params.CONFIG_TYPE)
namespace.setIsConsortiaSingleUi(params.CONSORTIA_SINGLE_UI)

TenantUi tenantUi = new TenantUi(namespace.getWorkspaceName(), commitHash, params.FOLIO_BRANCH as String)
tenantUi.setKongDomain(namespace.getDomains()['kong'])
tenantUi.setKeycloakDomain(namespace.getDomains()['keycloak'])
if (params.CONSORTIA) {
  tenantUi.setIsConsortia(params.CONSORTIA)
  tenantUi.setIsConsortiaSingleUi(params.CONSORTIA_SINGLE_UI)
}
tenantUi.removeUIComponents = removeUIComponents

EurekaTenant tenant = new EurekaTenant(params.TENANT_ID)
  .withTenantUi(tenantUi)
  .withConfiguration(new OkapiConfig())


ansiColor('xterm') {
  podTemplates.rancherAgent {
    stage('Ini') {
      buildName "#${namespace.getWorkspaceName()}.${env.BUILD_ID}"
      buildDescription "Branch: ${params.FOLIO_BRANCH}\nConsortia: ${params.CONSORTIA}\nLinked-data: ${params.LINKED_DATA}"
    }

    stage('Fetch data') {
      namespace.addDeploymentConfig(folioTools.getPipelineBranch())
      namespace.addTenant(tenant)
    }
  }

  folioUI.buildAndDeploy(namespace, tenant, params.ECS_CCL)
}
